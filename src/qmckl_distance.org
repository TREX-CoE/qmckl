#+begin_src elisp :results none :noexport:
(org-babel-lob-ingest "./orglib")
#+end_src

** Computation of distances

 Function for the computation of distances between particles.

 3 files are produced:
 - a source file : =qmckl_distance.f90=
 - a C test file : =test_qmckl_distance.c=
 - a Fortran test file : =test_qmckl_distance_f.f90=

**** Headers                                                       :noexport:
     #+BEGIN_SRC C :comments link :tangle test_qmckl_distance.c
#include "qmckl.h"
#include "munit.h"
MunitResult test_qmckl_distance() {
  qmckl_context context;
  context = qmckl_context_create();

     #+END_SRC


*** Squared distance

**** ~qmckl_distance_sq~
     :PROPERTIES:
     :CRetType: qmckl_exit_code
     :FRetType: integer
     :Name:     qmckl_distance_sq
     :END:

     Computes the matrix of the squared distances between all pairs of
     points in two sets, one point within each set:
     \[
     C_{ij} = \sum_{k=1}^3 (A_{k,i}-B_{k,j})^2
     \]

***** Arguments

      #+NAME: args
    | qmckl_context | context      | in  | Global state                                 |
    | char          | transa       | in  | Array ~A~ is ~N~: Normal, ~T~: Transposed    |
    | char          | transb       | in  | Array ~B~ is ~N~: Normal, ~T~: Transposed    |
    | int64_t       | m            | in  | Number of points in the first set            |
    | int64_t       | n            | in  | Number of points in the second set           |
    | double        | A[3][lda]    | in  | Array containing the $m \times 3$ matrix $A$ |
    | int64_t       | lda          | in  | Leading dimension of array ~A~               |
    | double        | B[3][ldb]  ] | in  | Array containing the $n \times 3$ matrix $B$ |
    | int64_t       | ldb          | in  | Leading dimension of array ~B~               |
    | double        | C[n][ldc]    | out | Array containing the $m \times n$ matrix $C$ |
    | int64_t       | ldc          | in  | Leading dimension of array ~C~               |


***** Requirements

      - ~context~ is not 0
      - ~m~ > 0
      - ~n~ > 0
      - ~lda~ >= 3 if ~transa~ is ~N~
      - ~lda~ >= m if ~transa~ is ~T~
      - ~ldb~ >= 3 if ~transb~ is ~N~
      - ~ldb~ >= n if ~transb~ is ~T~
      - ~ldc~ >= m 
      - ~A~ is allocated with at least $3 \times m \times 8$ bytes
      - ~B~ is allocated with at least $3 \times n \times 8$ bytes
      - ~C~ is allocated with at least $m \times n \times 8$ bytes

***** Performance 

      This function might be more efficient when ~A~ and ~B~ are
      transposed.

#+CALL: generate_header(d=args,rettyp=get_value("CRetType"),fname=get_value("Name"))

#+RESULTS:
#+begin_src C :tangle qmckl.h :comments org

qmckl_exit_code qmckl_distance_sq ( const qmckl_context context,
const char transa,
const char transb,
const int64_t m,
const int64_t n,
const double* A,
const int64_t lda,
const double* B,
const int64_t ldb,
const double* C,
const int64_t ldc )

#+end_src

***** Source
      #+NAME: argsF
    | integer*8 | context      | in  | Global state                                 |
    | character | transa       | in  | Array ~A~ is ~N~: Normal, ~T~: Transposed    |
    | character | transb       | in  | Array ~B~ is ~N~: Normal, ~T~: Transposed    |
    | integer*8 | m            | in  | Number of points in the first set            |
    | integer*8 | n            | in  | Number of points in the second set           |
    | real*8    | A[lda][*]    | in  | Array containing the $m \times 3$ matrix $A$ |
    | integer*8 | lda          | in  | Leading dimension of array ~A~               |
    | real*8    | B[ldd][*]    | in  | Array containing the $n \times 3$ matrix $B$ |
    | integer*8 | ldb          | in  | Leading dimension of array ~B~               |
    | real*8    | C[ldc][*]    | out | Array containing the $m \times n$ matrix $C$ |
    | integer*8 | ldc          | in  | Leading dimension of array ~C~               |

      #+BEGIN_SRC f90 :tangle qmckl_distance.f90
integer function qmckl_distance_sq_f(context, transa, transb, m, n, A, LDA, B, LDB, C, LDC) result(info)
  implicit none
  integer*8  , intent(in)  :: context
  character  , intent(in)  :: transa, transb
  integer*8  , intent(in)  :: m, n
  integer*8  , intent(in)  :: lda
  real*8     , intent(in)  :: A(lda,*)
  integer*8  , intent(in)  :: ldb
  real*8     , intent(in)  :: B(ldb,*)
  integer*8  , intent(in)  :: ldc
  real*8     , intent(out) :: C(ldc,*)

  integer*8 :: i,j
  real*8    :: x, y, z
  integer   :: transab

  info = 0

  if (context == 0_8) then
     info = -1
     return
  endif

  if (m <= 0_8) then
     info = -2
     return
  endif

  if (n <= 0_8) then
     info = -3
     return
  endif

  if (transa == 'N' .or. transa == 'n') then
     transab = 0
  else if (transa == 'T' .or. transa == 't') then
     transab = 1
  else
     transab = -100
  endif

  if (transb == 'N' .or. transb == 'n') then
     continue
  else if (transa == 'T' .or. transa == 't') then
     transab = transab + 2
  else
     transab = -100
  endif

  if (transab < 0) then
     info = -4
     return 
  endif

  if (iand(transab,1) == 0 .and. LDA < 3) then
     info = -5
     return
  endif

  if (iand(transab,1) == 1 .and. LDA < m) then
     info = -6
     return
  endif

  if (iand(transab,2) == 0 .and. LDA < 3) then
     info = -6
     return
  endif

  if (iand(transab,2) == 2 .and. LDA < m) then
     info = -7
     return
  endif


  select case (transab)
     
  case(0)

     do j=1,n
        do i=1,m
           x = A(1,i) - B(1,j)
           y = A(2,i) - B(2,j)
           z = A(3,i) - B(3,j)
           C(i,j) = x*x + y*y + z*z
        end do
     end do

  case(1)

     do j=1,n
        do i=1,m
           x = A(i,1) - B(1,j)
           y = A(i,2) - B(2,j)
           z = A(i,3) - B(3,j)
           C(i,j) = x*x + y*y + z*z
        end do
     end do

  case(2)

     do j=1,n
        do i=1,m
           x = A(1,i) - B(j,1)
           y = A(2,i) - B(j,2)
           z = A(3,i) - B(j,3)
           C(i,j) = x*x + y*y + z*z
        end do
     end do

  case(3)

     do j=1,n
        do i=1,m
           x = A(i,1) - B(j,1)
           y = A(i,2) - B(j,2)
           z = A(i,3) - B(j,3)
           C(i,j) = x*x + y*y + z*z
        end do
     end do

  end select
  
end function qmckl_distance_sq_f
      #+END_SRC

***** C interface                                                  :noexport:
#+CALL: generate_c_interface(d=argsF,rettyp=get_value("FRetType"),fname=get_value("Name"))

#+RESULTS:
#+begin_src f90 :tangle (fname).f :comments org

integer(c_int32_t)  function qmckl_distance_sq(context , transa , transb , m , n , A , lda , B , ldb , C , ldc) &
 bind(C) result(info)
 use, intrinsic :: iso_c_binding
 implicit none
integer(c_int32_t)  , intent(in) , value :: context
character(c_char)  , intent(in) , value :: transa
character(c_char)  , intent(in) , value :: transb
integer(c_int32_t)  , intent(in) , value :: m
integer(c_int32_t)  , intent(in) , value :: n
real(c_float)  , intent(in) ,       :: A
integer(c_int32_t)  , intent(in) , value :: lda
real(c_float)  , intent(in) ,       :: B
integer(c_int32_t)  , intent(in) , value :: ldb
real(c_float)  , intent(out) ,       :: C
integer(c_int32_t)  , intent(in) , value :: ldc
integer(c_int32_t), external :: ldc
info = qmckl_distance_sq_f(context , transa , transb , m , n , A , lda , B , ldb , C , ldc)
end function qmckl_distance_sq

#+end_src

      #+BEGIN_SRC f90 :tangle qmckl_distance.f90
integer(c_int32_t) function qmckl_distance_sq(context, transa, transb, m, n, A, LDA, B, LDB, C, LDC) &
     bind(C) result(info)
  use, intrinsic :: iso_c_binding
  implicit none
  integer (c_int64_t) , intent(in) , value :: context
  character (c_char)  , intent(in) , value :: transa, transb
  integer (c_int64_t) , intent(in) , value :: m, n
  integer (c_int64_t) , intent(in) , value :: lda
  real    (c_double)  , intent(in)         :: A(lda,3)
  integer (c_int64_t) , intent(in) , value :: ldb
  real    (c_double)  , intent(in)         :: B(ldb,3)
  integer (c_int64_t) , intent(in) , value :: ldc
  real    (c_double)  , intent(out)        :: C(ldc,n)

  integer, external :: qmckl_distance_sq_f
  info = qmckl_distance_sq_f(context, transa, transb, m, n, A, LDA, B, LDB, C, LDC)
end function qmckl_distance_sq
      #+END_SRC

      #+BEGIN_SRC f90 :tangle qmckl_f.f90
  interface
     integer(c_int32_t) function qmckl_distance_sq(context, transa, transb, m, n, A, LDA, B, LDB, C, LDC) &
          bind(C) 
       use, intrinsic :: iso_c_binding
       implicit none
       integer (c_int64_t) , intent(in) , value :: context
       character (c_char)  , intent(in) , value :: transa, transb
       integer (c_int64_t) , intent(in) , value :: m, n
       integer (c_int64_t) , intent(in) , value :: lda
       integer (c_int64_t) , intent(in) , value :: ldb
       integer (c_int64_t) , intent(in) , value :: ldc
       real    (c_double)  , intent(in)         :: A(lda,3)
       real    (c_double)  , intent(in)         :: B(ldb,3)
       real    (c_double)  , intent(out)        :: C(ldc,n)
     end function qmckl_distance_sq
  end interface
      #+END_SRC

***** Test                                                         :noexport:
      #+BEGIN_SRC f90 :tangle test_qmckl_distance_f.f90
integer(c_int32_t) function test_qmckl_distance_sq(context) bind(C)
  use qmckl
  implicit none
  integer(c_int64_t), intent(in), value :: context

  double precision, allocatable :: A(:,:), B(:,:), C(:,:)
  integer*8                     :: m, n, LDA, LDB, LDC
  double precision              :: x
  integer*8                     :: i,j 

  m = 5
  n = 6
  LDA = m
  LDB = n
  LDC = 5

  allocate( A(LDA,m), B(LDB,n), C(LDC,n) )

  do j=1,m
     do i=1,m
        A(i,j) = -10.d0 + dble(i+j)
     end do
  end do
  do j=1,n
     do i=1,n
        B(i,j) = -1.d0 + dble(i*j)
     end do
  end do

  test_qmckl_distance_sq = qmckl_distance_sq(context, 'X', 't', m, n, A, LDA, B, LDB, C, LDC)
  if (test_qmckl_distance_sq == 0) return 

  test_qmckl_distance_sq = qmckl_distance_sq(context, 't', 'X', m, n, A, LDA, B, LDB, C, LDC)
  if (test_qmckl_distance_sq == 0) return 

  test_qmckl_distance_sq = qmckl_distance_sq(context, 'T', 't', m, n, A, LDA, B, LDB, C, LDC)
  if (test_qmckl_distance_sq /= 0) return 

  test_qmckl_distance_sq = -1

  do j=1,n
     do i=1,m
        x =  (A(i,1)-B(j,1))**2 + &
             (A(i,2)-B(j,2))**2 + &
             (A(i,3)-B(j,3))**2
        if ( dabs(1.d0 - C(i,j)/x) > 1.d-14 ) return
     end do
  end do
  
  test_qmckl_distance_sq = qmckl_distance_sq(context, 'n', 'T', m, n, A, LDA, B, LDB, C, LDC)
  if (test_qmckl_distance_sq /= 0) return 

  test_qmckl_distance_sq = -1

  do j=1,n
     do i=1,m
        x =  (A(1,i)-B(j,1))**2 + &
             (A(2,i)-B(j,2))**2 + &
             (A(3,i)-B(j,3))**2
        if ( dabs(1.d0 - C(i,j)/x) > 1.d-14 ) return
     end do
  end do

  test_qmckl_distance_sq = qmckl_distance_sq(context, 'T', 'n', m, n, A, LDA, B, LDB, C, LDC)
  if (test_qmckl_distance_sq /= 0) return 

  test_qmckl_distance_sq = -1

  do j=1,n
     do i=1,m
        x =  (A(i,1)-B(1,j))**2 + &
             (A(i,2)-B(2,j))**2 + &
             (A(i,3)-B(3,j))**2
        if ( dabs(1.d0 - C(i,j)/x) > 1.d-14 ) return
     end do
  end do

  test_qmckl_distance_sq = qmckl_distance_sq(context, 'n', 'N', m, n, A, LDA, B, LDB, C, LDC)
  if (test_qmckl_distance_sq /= 0) return 

  test_qmckl_distance_sq = -1

  do j=1,n
     do i=1,m
        x =  (A(1,i)-B(1,j))**2 + &
             (A(2,i)-B(2,j))**2 + &
             (A(3,i)-B(3,j))**2
        if ( dabs(1.d0 - C(i,j)/x) > 1.d-14 ) return
     end do
  end do
  
  test_qmckl_distance_sq = 0
  
  deallocate(A,B,C)
end function test_qmckl_distance_sq
      #+END_SRC
    
      #+BEGIN_SRC C :comments link :tangle test_qmckl_distance.c
int test_qmckl_distance_sq(qmckl_context context);
munit_assert_int(0, ==, test_qmckl_distance_sq(context));
      #+END_SRC
*** End of files                                                   :noexport:

    #+BEGIN_SRC C :comments link :tangle test_qmckl_distance.c
  if (qmckl_context_destroy(context) != QMCKL_SUCCESS)
    return QMCKL_FAILURE;
  return MUNIT_OK;
}

    #+END_SRC
  

 # -*- mode: org -*-
 # vim: syntax=c
